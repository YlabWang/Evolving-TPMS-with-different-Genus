// FRDA Genus level 2

parameter asize = 1.8 // for period killing; exact value  1.842507083915894

constraint 3 formula: x = z
constraint 4 formula: x + y = 0
constraint 5 formula: y = x
constraint 6 formula: z = 1
constraint 7 formula: y = 0

vertices
1   0 0 0 fixed
2   -1 -1 0 fixed
3   -1-asize -1+asize 0 fixed
4   -2*asize 0 0 fixed
5   0 0 -2*asize fixed

edges
1  1 2 fixed
2  2 3 fixed
3  3 4 fixed
4  4 5 fixed
5  5 1 fixed

faces
1  -5 -4 -3 -2 -1

read
hessian_normal

gg   := { refine edge where valence == 1; g 5; r; g 10; u; V;
          g5; hessian;hessian; 
          g 5; hessian; hessian;
          r; g 5; u; V; u; g 5; hessian; hessian;
          g5; hessian;hessian; 
          r; g 5; u; V; u; g 5; hessian; hessian;
          g 5; V; u; V; hessian; hessian;
          r; g 5; u; V; u; g 5; hessian; hessian;
          g 5; V; u; V; hessian; hessian;
        }

calc := {
          edge2dy := sum(edge ee where original==2,
           sum(ee.facet ff, (ff.z*ee.x-ff.x*ee.z)/sqrt(ff.x^2+ff.y^2+ff.z^2)));

          printf " edge2dy: %g   \n",edge2dy;
        }

read "adjoint.cmd"

adj := { 
         adjoint;
       }


frame := {
    unfix vertices; unfix edges;
    aa := vertex[2].y; set vertex y y-aa;
    bb := minimum(min(vertex,x-y),min(vertex,x+y)); set vertex x x-bb;
    cc := min(vertex,z-x); set vertex z z-cc;
    mag := max(vertex,z);
    set vertex x x/mag;
    set vertex y y/mag;
    set vertex z z/mag;
    foreach edge ee where original==1 do
    { set ee.vertex constraint 4; set ee constraint 4; };
    foreach edge ee where original==2 do
    { set ee constraint 5; set ee.vertex constraint 5; };
    foreach edge ee where original==3 do
    { set ee constraint 4; set ee.vertex constraint 4; };
    foreach edge ee where original==4 do
    { set ee constraint 3; set ee.vertex constraint 3; };
    foreach edge ee where original==5 do
    { set ee constraint 6; set ee.vertex constraint 6; };
}

// For saving adjoint surface, with transforms
//  Should redirect output to file.
dumpit := { 
   printf "// adjoint of %s.\n",datafilename;
   list topinfo;
   printf "\nview_transform_generators 3\n";
   printf " 0 1 0 0   1 0 0 0   0 0 1 0  0 0 0 1\n";  // x y swap
   printf " 0 -1 0 0   -1 0 0 0   0 0 1 0  0 0 0 1\n";  // x+y mirror
   printf "0 0 1 0   0 1 0 0   1 0 0 0   0 0 0 1\n"; // x=z mirror
   printf "\nVertices\n";
   list vertices;
   printf "\nEdges\n";
   list edges;
   printf "\nFaces\n";
   list facets;
   list bottominfo;
}



// To get true asize after evolving after adjointing
true_asize := { printf "True asize: %20.15f\n",
   sum(edge where original == 2,length)/sum(edge where original==1,length); }

// command to show full cell in rhombic dodecahedron
showrhombic := { transform_expr "abcabch"; transforms on;                
                 show edge where valence <= 1;
                 show_trans "R";
	      }
rhombic_edges := {
      va := new_vertex(0,0,0);
      vb := new_vertex(1,-1,1);
      newe1 := new_edge(va,vb);
      vc := new_vertex(1,1,1);
      newe2 := new_edge(va,vc);
      set edge[newe1] bare;
      set edge[newe1] fixed;
      set edge[newe1] no_refine;
      set edge[newe2] bare;
      set edge[newe2] fixed;
      set edge[newe2] no_refine;

}

// command to show full cell in cube
showcube := { transform_expr "abcabc"; transforms on; 
              set edge color black;
              show edge where valence == 1;
              show_trans "R";
	      }

setcolor := { set facet frontcolor yellow }

// To show just one fundamental region, do "transforms off".
// To show tetrahedron outline again, do "show edges".

// A typical evolution
gogo := { gg; adj; frame; g 5; V; V; g 5; show_trans "R"; hessian; hessian;
        }
